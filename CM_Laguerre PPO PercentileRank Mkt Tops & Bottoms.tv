//Created by ChrisMoody on 11/03/2014...Original PPO Code Created by TheLark
//Thanks to coondawg71 and ChartArt for Great Insights
//Great for Spotting Tops & Bottoms.
study(title = "CM_Laguerre PPO PercentileRank Mkt Tops & Bottoms", shorttitle="CM_Laguerre PPO PctRank Tops-Bottoms", overlay=false, precision=1)
pctile = input(90, title="Percentile Threshold Extreme Value, Exceeding Creates Colored Histogram")
wrnpctile = input(70, title="Percentile Threshold Warning Value, Exceeding Creates Colored Histogram")
Short = input(0.4, title="PPO Setting")
Long = input(0.8, title="PPO Setting")
lkbT = input(200,title="Look Back Period For 'Tops' Percent Rank is based off of?")
lkbB = input(200,title="Look Back Period For 'Bottoms' Percent Rank is based off of?")
sl=input(true,title="Show Threshold Line?")
swl=input(true,title="Show Warning Threshold Line?")

//Laguerre PPO Code from TheLark
lag(g, p) =>
    L0 = (1 - g)*p+g*nz(L0[1])
    L1 = -g*L0+nz(L0[1])+g*nz(L1[1])
    L2 = -g*L1+nz(L1[1])+g*nz(L2[1])
    L3 = -g*L2+nz(L2[1])+g*nz(L3[1])
    f = (L0 + 2*L1 + 2*L2 + L3)/6
    f
lmas = lag(Short, hl2)
lmal = lag(Long, hl2)

pctileB = pctile * -1
wrnpctileB = wrnpctile * -1

//PPO Plot
ppoT = (lmas-lmal)/lmal*100
ppoB = (lmal - lmas)/lmal*100
//PercentRank of PPO
pctRankT = percentrank(ppoT, lkbT)
pctRankB = percentrank(ppoB, lkbB) * -1
//Color Definition of Columns
colT = pctRankT >= pctile ? red : pctRankT >= wrnpctile and pctRankT < pctile ? orange : gray
colB = pctRankB <= pctileB ? lime : pctRankB <= wrnpctileB and pctRankB > pctileB ? green : silver
//Plot Statements.
plot(pctRankT,title="Percentile Rank Columns", color=colT, style=columns, linewidth=2)
plot(sl and pctile ? pctile : na, title="Extreme Move Percentile Threshold Line", color=red, style=linebr, linewidth=4)
plot(swl and wrnpctile ? wrnpctile : na, title="Warning Percentile Threshold Line", color=orange, style=line, linewidth=4)

plot(pctRankB,title="Percentile Rank Columns", color=colB, style=columns, linewidth=2)
plot(sl and pctileB ? pctileB : na, title="Extreme Move Percentile Threshold Line", color=lime, style=linebr, linewidth=4)
plot(swl and wrnpctileB ? wrnpctileB : na, title="Warning Percentile Threshold Line", color=green, style=line, linewidth=4)
plot(0, title="0 Line Circles Plot", style=circles, linewidth=4, color=silver)
plot(0, title="0 Line-Line Plot", style=linebr, linewidth=4, color=gray)
